%SCRIPT XTEACHI
%% Respond to button presses from window setup by XTEACH
%%
%%
%  VARIABLE CHARACTER CDIR CFDIR
%% The variables needed by XQUESTIO
% VARIABLE CHARACTER QTITLE QLINE1 QLINE2 BUTTOK BUTTXX 
% VARIABLE LOGICAL   ANSWER T
%%
%  VERIFY GI_BXX MNCO6 MNCO3PY3 YLID CUOAC PARA
%  GET SILENT NOSTORE ABBREVIATED 'Continue?' 'GI_BXX'
%%
%  CASE VALUE
%    BLOCK  %GI_BXX% 
%% Close window
^^CO DISPOSE XTEACHW

{I Teaching window closed. Choose Teaching datasets from the Help meun to re-open it.

%      FINISH
%    END BLOCK  
%    BLOCK %MNCO6%
%      EVALUATE CDIR = 'CRYSDIR:demo/teach/moco6/'
%    END BLOCK  
%    BLOCK %MNCO3PY3%
%      EVALUATE CDIR = 'CRYSDIR:demo/teach/moco3py3/'
%    END BLOCK  
%    BLOCK %YLID%
%      EVALUATE CDIR = 'CRYSDIR:demo/teach/ylid/'
%    END BLOCK  
%    BLOCK %CUOAC%
%      EVALUATE CDIR = 'CRYSDIR:demo/teach/cuoac/'
%    END BLOCK  
%    BLOCK %PARA%
%      EVALUATE CDIR = 'CRYSDIR:demo/teach/para/'
%    END BLOCK  
%  END CASE
%% Re-open CRYSTALS in correct folder
%  EVALUATE CFDIR = GETENV ( CDIR ) 
%  IF ( .NOT. ( FILEEXISTS CFDIR ) ) THEN
%     TRANSFER '{E Cannot find "' // CFDIR // '"' TO DISPLAY
 {I Reinstall CRYSTALS to restore the workshop data.
%     FINISH
%  END IF
%%
%%
%% If crfilev2.dsc present, offer to delete before continuing.
%  IF ( FILEEXISTS ( CFDIR // 'crfilev2.dsc'  ) ) THEN
%      EVALUATE QTITLE = 'Reset teaching example?'
%      EVALUATE QLINE1 = 'There is previous work present. Open existing file or reset?'
%      EVALUATE QLINE2 = CFDIR
%      EVALUATE BUTTOK = '&Open existing work'
%      EVALUATE BUTTXX = '&Reset'
%      COPY '#SCRIPT XQUESTIO'
%      IF ANSWER .EQ. FALSE THEN
%        EVALUATE T = FILEDELETE ( CFDIR // 'crfilev2.dsc' )
%        IF .NOT. T THEN
%% crfilev2.dsc could be the current dsc file. Switch to new file (temp.dsc) and try again.
{I Could not delete .dsc. Trying again. 
%          EVALUATE T = FILEDELETE ( CFDIR // 'temp.dsc' )
%          COPY '#PURGE temp'
%          COPY 'ACTION REOPEN=YES'
%          COPY 'END'
%          EVALUATE T = FILEDELETE ( CFDIR // 'crfilev2.dsc' )
%          SHOW T
%        END IF
{I  Teaching data reset.
%      END IF
%  END IF
%%
^^WI  SET _MAIN TEXT=
%     TRANSFER "^^WI 'Crystals - " // CFDIR // "'" TO DISPLAY
^^CR
%     COPY '#SCRIPT TABRESET'
^^CO DISPOSE XTEACHW
%     TRANSFER "^^CO RESTART '" // CFDIR // -
                     "' NEWFILE=crfilev2.dsc" TO DISPLAY
%%
%% MAKE SURE THE GUI HAS RECEIVED THESE COMMANDS BEFORE CLOSING DOWN!
%% We query the value of any object by name.
%% A value will not return until the GUI has caught up.
%%
^^??  _MAIN EXISTS
%     GET NOSTORE TEXT 'About to restart in new directory...' ' '
^^CO  SET _MAINTEXTOUTPUT EMPTY
%     COPY '#END'
%% Possibly redundant:
%     FINISH
%   END BLOCK
%   BLOCK
^^CO  DISPOSE QUERY
%     FINISH
%   END BLOCK
